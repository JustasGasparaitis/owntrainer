apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'dagger.hilt.android.plugin'


def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildFeatures {
        dataBinding true
    }
    defaultConfig {
        applicationId "com.gasparaiciukas.owntrainer"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 1
        versionName "0.1.0"
        testInstrumentationRunner "com.gasparaiciukas.owntrainer.HiltTestRunner"
        buildConfigField("String", "API_KEY", apikeyProperties['API_KEY'])
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    packagingOptions {
        resources {
            excludes += ['/META-INF/AL2.0', '/META-INF/LGPL2.1']
        }
    }
}

dependencies {
    // Java Library Desugaring
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$rootProject.desugaringVersion"

    // Core KTX
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"

    // Fragment KTX
    implementation "androidx.fragment:fragment-ktx:$rootProject.fragmentVersion"

    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$rootProject.lifecycleVersion"

    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$rootProject.lifecycleVersion"

    // AppCompat
    implementation "androidx.appcompat:appcompat:$rootProject.appCompatVersion"

    // RecyclerView
    implementation "androidx.recyclerview:recyclerview:$rootProject.recyclerViewVersion"

    // ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$rootProject.navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$rootProject.navigationVersion"

    // Transition
    implementation "androidx.transition:transition-ktx:$rootProject.transitionVersion"

    // Material UI
    implementation "com.google.android.material:material:$rootProject.materialVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.retrofitVersion"
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofitVersion"

    // Circular progress bar
    implementation "com.mikhaellopez:circularprogressbar:$rootProject.circularProgressBarVersion"

    // Pie chart
    implementation "com.github.PhilJay:MPAndroidChart:$rootProject.pieChartVersion"

    // App Intro
    implementation "com.github.AppIntro:AppIntro:$rootProject.appIntroVersion"

    // Timber for Logging
    implementation "com.jakewharton.timber:timber:$rootProject.timberVersion"

    // Hilt
    implementation "com.google.dagger:hilt-android:$rootProject.hiltVersion"
    kapt "com.google.dagger:hilt-compiler:$rootProject.hiltVersion"

    // Room
    implementation "androidx.room:room-runtime:$rootProject.roomVersion"
    kapt "androidx.room:room-compiler:$rootProject.roomVersion"
    implementation "androidx.room:room-ktx:$rootProject.roomVersion"

    // Splash Screen
    implementation "androidx.core:core-splashscreen:$rootProject.splashScreenVersion"

    // Testing
    implementation "androidx.test:core:$rootProject.testCoreVersion"
    testImplementation "junit:junit:$rootProject.junitCoreVersion"
    testImplementation "org.hamcrest:hamcrest-all:$rootProject.hamcrestVersion"
    testImplementation "androidx.arch.core:core-testing:$rootProject.archCoreTestingVersion"
    testImplementation "org.robolectric:robolectric:$rootProject.robolectricVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.coroutineTestVersion"
    testImplementation "com.google.truth:truth:$rootProject.truthVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"

    androidTestImplementation "junit:junit:$rootProject.junitCoreVersion"
    androidTestImplementation "com.linkedin.dexmaker:dexmaker-mockito:$rootProject.mockitoDexmakerVersion"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$rootProject.coroutineTestVersion"
    androidTestImplementation "androidx.arch.core:core-testing:$rootProject.archCoreTestingVersion"
    androidTestImplementation "com.google.truth:truth:$rootProject.truthVersion"
    androidTestImplementation "androidx.test.ext:junit:$rootProject.junitAndroidXVersion"

    // updating Espresso to 3.4.0 will break tests
    //noinspection GradleDependency
    androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.espressoVersion"
    //noinspection GradleDependency
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$rootProject.espressoVersion"


    androidTestImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$rootProject.hiltVersion"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$rootProject.hiltVersion"
    debugImplementation "androidx.fragment:fragment-testing:$rootProject.fragmentTestingVersion"
    androidTestImplementation "androidx.navigation:navigation-testing:$rootProject.navigationVersion"
}
repositories {
    mavenCentral()
}